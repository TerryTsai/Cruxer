// Plugins
apply plugin: 'idea'
apply plugin: 'gulp'
apply plugin: 'application'
apply plugin: 'spring-boot'

sourceCompatibility = 1.8
targetCompatibility = 1.8

// Build Script
buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.0.RELEASE")
        classpath("be.filipblondeel.gradle:gradle-gulp-plugin:0.1")
    }
}

// Project Repositories
repositories {
    mavenCentral()
}

// Cruxer Packaging
jar {
    baseName = 'cruxer'
    version =  '0.0.1'
}

// Gulp Tasks
gulp_build.dependsOn 'installGulp'
gulp_build.dependsOn 'npmInstall'
build.dependsOn gulp_build
run.dependsOn gulp_build

// Deployment Tasks
task deployClean(type: Delete) {
    delete 'deploy'
}

task deployGather(dependsOn: ['deployClean', 'build']) << {
    copy {
        from 'build/libs'
        into 'deploy'
    }
    copy {
        from 'site'
        into 'deploy/site'
    }
}

task deployZip(type: Zip, dependsOn: ['deployGather']) {
    from 'deploy'
    baseName = 'Cruxer-Deployable'
}

// Dependencies
dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-data-rest'
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'org.springframework.boot:spring-boot-starter-thymeleaf'

    compile 'org.springframework:spring-context-support:4.2.3.RELEASE'

    compile 'javax.mail:javax.mail-api:1.5.5'
    compile 'com.h2database:h2:1.4.190'
    compile 'mysql:mysql-connector-java:5.1.38'
    compile 'org.projectlombok:lombok:1.16.6'
    compile 'net.sourceforge.nekohtml:nekohtml:1.9.22'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.springframework.security:spring-security-test'
    testCompile 'com.jayway.restassured:rest-assured:2.8.0'
    testCompile 'com.jayway.jsonpath:json-path:2.1.0'

}

// Gradle Wrapper
task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}